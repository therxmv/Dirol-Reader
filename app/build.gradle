plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

android {
    namespace 'com.therxmv.dirolreader'
    compileSdk 33

    defaultConfig {
        applicationId "com.therxmv.dirolreader"
        minSdk 23
        targetSdk 33
        versionCode 111
        versionName "1.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())

        buildConfigField "String", "API_HASH", "\"${properties.getProperty("API_HASH")}\""
        buildConfigField "Integer", "API_ID", "${properties.getProperty("API_ID")}"
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Dirol-Reader-${variant.versionName}.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(path: ':libtd')
    implementation project(path: ':ota-updates')

    // Gson
    implementation 'com.google.code.gson:gson:2.10.1'

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:32.2.0")
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation "com.google.firebase:firebase-analytics-ktx"

    // Markdown
    final def markwon_version = '4.6.2'

    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:html:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"

    implementation 'com.github.jeziellago:compose-markdown:0.3.4'

    // Exoplayer
    implementation 'androidx.media3:media3-exoplayer:1.1.0-rc01'
    implementation 'androidx.media3:media3-ui:1.1.0-rc01'

    // Collapsing toolbar
    implementation 'me.onebone:toolbar-compose:2.3.5'

    // Paging
    implementation "androidx.paging:paging-compose:3.2.0-beta01"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.6.0"

    // Room
    def roomVer = '2.5.1'
    implementation "androidx.room:room-runtime:$roomVer"
    kapt "androidx.room:room-compiler:$roomVer"

    // Hilt
    def hiltVer = '2.44.2'
    implementation "com.google.dagger:hilt-android:$hiltVer"
    kapt "com.google.dagger:hilt-compiler:$hiltVer"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.0"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation "androidx.compose.material:material:1.5.0-beta02"
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}